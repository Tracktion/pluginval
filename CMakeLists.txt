cmake_minimum_required(VERSION 3.15)

file(READ VERSION CURRENT_VERSION)
project(pluginval VERSION ${CURRENT_VERSION})

if (APPLE)
    # Target OS versions down to 10.11
    set (CMAKE_OSX_DEPLOYMENT_TARGET "10.11" CACHE INTERNAL "")
    
    # Uncomment to produce a universal binary
    # set(CMAKE_OSX_ARCHITECTURES arm64 x86_64)
endif()

# Adds all the module sources so they appear correctly in the IDE
set_property(GLOBAL PROPERTY USE_FOLDERS YES)
option(JUCE_ENABLE_MODULE_SOURCE_GROUPS "Enable Module Source Groups" ON)

option(PLUGINVAL_FETCH_JUCE "Fetch JUCE along with PluginVal" ON)

if(PLUGINVAL_FETCH_JUCE)
    add_subdirectory(modules/juce)
endif()

juce_set_vst2_sdk_path("Source/vst2-sdk")

juce_add_gui_app(pluginval
    BUNDLE_ID                       com.Tracktion.pluginval
    COMPANY_NAME                    Tracktion
    ICON_BIG                        "${CMAKE_CURRENT_SOURCE_DIR}/Source/binarydata/icon.png"
    HARDENED_RUNTIME_ENABLED        TRUE
    HARDENED_RUNTIME_OPTIONS        com.apple.security.cs.allow-unsigned-executable-memory com.apple.security.cs.disable-library-validation com.apple.security.get-task-allow)

juce_generate_juce_header(pluginval)

target_compile_features(pluginval PRIVATE cxx_std_17)

set_target_properties(pluginval PROPERTIES
    C_VISIBILITY_PRESET hidden
    CXX_VISIBILITY_PRESET hidden)

set(SourceFiles
    Source/CommandLine.h
    Source/CrashHandler.h
    Source/MainComponent.h
    Source/PluginTests.h
    Source/TestUtilities.h
    Source/Validator.h
    Source/CommandLine.cpp
    Source/CrashHandler.cpp
    Source/Main.cpp
    Source/MainComponent.cpp
    Source/PluginTests.cpp
    Source/tests/BasicTests.cpp
    Source/tests/BusTests.cpp
    Source/tests/ParameterFuzzTests.cpp
    Source/TestUtilities.cpp
    Source/Validator.cpp)

target_sources(pluginval PRIVATE ${SourceFiles})
source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR}/Source PREFIX Source FILES ${SourceFiles})

target_compile_definitions(pluginval PRIVATE
    JUCE_PLUGINHOST_VST=1
    JUCE_PLUGINHOST_AU=1
    JUCE_PLUGINHOST_LADSPA=1
    JUCE_PLUGINHOST_VST3=1
    JUCE_USE_CURL=0
    JUCE_WEB_BROWSER=0
    JUCER_ENABLE_GPL_MODE=1
    JUCE_DISPLAY_SPLASH_SCREEN=0
    JUCE_MODAL_LOOPS_PERMITTED=1
    JUCE_REPORT_APP_USAGE=0
    JUCE_GUI_BASICS_INCLUDE_XHEADERS=1
    VERSION="${CURRENT_VERSION}")

target_link_libraries(pluginval PRIVATE
    juce::juce_audio_devices
    juce::juce_audio_processors
    juce::juce_audio_utils
    juce::juce_recommended_warning_flags)

if (${CMAKE_SYSTEM_NAME} MATCHES "Linux")
    target_link_libraries(pluginval PRIVATE
        -static-libstdc++)
endif()

set (cmdline_docs_out "${CMAKE_CURRENT_LIST_DIR}/docs/Command line options.md")

add_custom_command (OUTPUT "${cmdline_docs_out}"
                    COMMAND pluginval --help > "${cmdline_docs_out}"
                    COMMENT "Regenerating Command line options.md..."
                    USES_TERMINAL)

add_custom_target (PluginvalDocs DEPENDS "${cmdline_docs_out}")
